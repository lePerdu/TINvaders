;
; menu.z80
;
; Copyright (c) 2017 Zach Peltzer
; Subject to the MIT License
;

MENU_START_GAME .equ 1
MENU_HIGH_SCORES .equ 2
MENU_INSTRUCTIONS .equ 3
MENU_ABOUT .equ 4
MENU_QUIT .equ 5

MENU_NUM_ITEMS .equ 5

SELECTION_X .equ 8
SELECTION_Y .equ 11
MENU_ITEM_X .equ 16
MENU_Y .equ 10
MENU_ITEM_HEIGHT .equ 8

SEL_SPRITE_HEIGHT .equ 5

; Macro to get the column from the width of a string in pixels to center the
; string
#define CENTER_COL(WIDTH) ((SCREEN_WIDTH - (WIDTH)) / 2)


menu_text:
    .db  0, CENTER_COL(35), " TINvaders",0
    .db 10, 16, "Start Game",0
    .db 18, 16, "High Scores",0
    .db 26, 16, "Instructions",0
    .db 34, 16, "About",0
    .db 42, 16, "Quit",0
MENU_NUM_TEXT_ENTRIES .equ 6

menu_selection:
	.db 1

menu_selection_sprite:
	.db %1100000
	.db %1100000
	.db %1110000
	.db %1100000
	.db %1100000


;
; Draws text from an array of (sort-of) structures in the form of:
;   row, column, null-terminated string
; The first line of the structure is inverted, to act as a header. Text
; is drawn to the graphics buffer, but the buffer is not displayed.
;
; Input:
;   hl - Address of the data
;   b - The number of entries
;
menu_draw_text:
    res fracDrawLFont, (iy + fontFlags)

    ; Make header inverted
    set textInverse, (iy + textFlags)

_mdt_draw_loop:
    ; Draw the header separately, inverted
    ld a, (hl)
    ld (penRow), a
    inc hl
    ld a, (hl)
    ld (penCol), a
    inc hl

    BCALL(_vPutS)
    ; hl set to byte after string

    ; Uninvert (from the inverted header)
    ; Even though this is redundant, it reduces duplicate code
    res textInverse, (iy + textFlags)
    djnz _mdt_draw_loop
_mdt_draw_loop_end:

    ret


menu:
	BCALL(_grBufClr)

    ld hl, menu_text
    ld b, MENU_NUM_TEXT_ENTRIES
    call menu_draw_text

	ld a, 1
	ld (menu_selection), a

	call menu_render_selection

_menu_render:
_mr_selection_render:
	call safe_copy_pss
_menu_get_input:
	BCALL(_getKey)
	cp kClear
	jp z, quit
	cp kQuit
	jp z, quit
	cp kUp
	jr z, menu_move_up
	cp kDown
	jr z, menu_move_down
	cp kEnter
	jr z, menu_select

	jr _menu_render


menu_move_down:
	call menu_render_selection
	ld a, (menu_selection)
	cp MENU_NUM_ITEMS
	jr nz, _move_down_norm
	xor a
_move_down_norm:
	inc a

	ld (menu_selection), a
	call menu_render_selection
	jr _menu_render


menu_move_up:
	call menu_render_selection
	ld a, (menu_selection)
	cp 1
	jr nz, _move_up_norm
	ld a, MENU_NUM_ITEMS + 1
_move_up_norm:
	dec a

	ld (menu_selection), a
	call menu_render_selection
	jr _menu_render


menu_render_selection:
	ld d, SELECTION_X
	ld e, SELECTION_Y
	ld a, (menu_selection)
	dec a
	sla a ; MENU_ITEM_HEIGHT = 8
	sla a
	sla a
	add a, e
	ld e, a
	ld ix, menu_selection_sprite
	ld b, SEL_SPRITE_HEIGHT
	;call put_sprite_xor
	;ret
	jp put_sprite_xor


menu_select:
	ld a, (menu_selection)
	cp MENU_START_GAME
	ret z
	cp MENU_HIGH_SCORES
	jr z, menu_sel_high_scores
	cp MENU_INSTRUCTIONS
    jp z, menu_sel_instructions
	cp MENU_ABOUT
    jp z, menu_sel_about
	cp MENU_QUIT
	jp z, quit
	jp menu


menu_sel_high_scores:
	ld hl, 0
	ld (score), hl
	call scoreboard_check
	call scoreboard_display
	jp z, quit
	jp menu



instr_text:
    .db 0, CENTER_COL(42), " Instructions",0

    .db 10, 0, "Controls:",0
    .db 17, 8, "RIGHT/LEFT - Move",0
    .db 24, 8, "2ND - Fire",0
    .db 31, 8, "CLEAR - Quit",0

    .db 41, 0, "Scoring:",0
    .db 48, 8, "10, 20, and 40 points for",0
    .db 55, 8, "the 3 sizes of invaders",0
INSTR_NUM_ENTRIES .equ 8

menu_sel_instructions:
    BCALL(_grBufClr)

    ld hl, instr_text
    ld b, INSTR_NUM_ENTRIES

    call menu_draw_text
    call safe_copy_pss

_msi_get_input:
    BCALL(_getKey)
    cp kClear
    jp z, quit
    cp kQuit
    jp z, quit
    cp kEnter
    ;jr z, _msi_end
    ;jj _msi_get_input
    jr nz, _msi_get_input

_msi_end:
    jp menu


about_text:
    .db  0, CENTER_COL(19), " About",0
    .db 10, CENTER_COL(50), "TINvaders  v1.0",0
    .db 20, CENTER_COL(60), "Copyright (c) 2017",0
    .db 27, CENTER_COL(42), "Zach Peltzer",0
    .db 34, CENTER_COL(70), "Under the MIT License",0
    .db 44, CENTER_COL(85), "Fork on GitHub: github.com",0
    .db 51, CENTER_COL(80), "/lePerdu/TINvaders.git",0
ABOUT_NUM_ENTRIES .equ 7

menu_sel_about:
	BCALL(_grBufClr)

    ld hl, about_text
    ld b, ABOUT_NUM_ENTRIES

    call menu_draw_text
    call safe_copy_pss

_msa_get_input:
	BCALL(_getKey)
	cp kClear
	jp z, quit
	cp kQuit
	jp z, quit
	cp kEnter
    ;jr z, _msa_end
    ;jr _msa_get_input
    jr nz, _msa_get_input

_msa_end:
    jp menu

